module datapath #(
    param ENTRY_POINT: logic<16> = 16'h0140,
) (
    clk    : input   clock                      ,
    rst    : input   reset                      ,
    c_if   : modport control_if::datapath       ,
    mm_addr: output  micro1_machine_address_t   ,
    mm_din : input   micro1_machine_word_t      ,
    mm_dout: output  micro1_machine_word_t      ,
    if_dout: input   logic                   <8>,
    of_din : output  logic                   <8>,
) {
    // imports
    import $std::selector_pkg::*;

    import alu_pkg::*;
    import machine_data_pkg::*;
    import gpr_destination_selector_pkg::*;
    import ir_source_selector_pkg::*;
    import lbus_source_selector_pkg::*;
    import rbus_source_selector_pkg::*;
    import shifter_pkg::*;

    // constant value
    const CONSTANT_ZERO_16: logic<16> = 16'h0;

    // registers and wires
    var gpr        : micro1_machine_word_t         [8];
    var pc         : micro1_machine_word_t            ;
    var fsr        : logic                <4>         ;
    var mar        : micro1_machine_word_t            ;
    var op         : logic                <4>         ;
    var ra         : logic                <2>         ;
    var rb         : logic                <2>         ;
    var nd         : logic                <8>         ;
    var rap        : logic                <2>         ;
    var rbp        : logic                <2>         ;
    var rbm        : logic                <2>         ;
    var lbus       : logic                <16>        ;
    var rbus       : logic                <16>        ;
    var sbus       : logic                <16>        ;
    var inbus      : micro1_machine_word_t            ;
    var lbus_source: logic                <16, 16>    ;
    var rbus_source: logic                <16, 16>    ;

    // submodules
    inst mux_for_lbus: $std::mux #(
        WIDTH  : 16                   ,
        ENTRIES: 16                   ,
        KIND   : selector_kind::BINARY,
    ) (
        i_data  : lbus_source              ,
        i_select: c_if.lbus_source_selector,
        o_data  : lbus                     ,
    );

    inst mux_for_rbus: $std::mux #(
        WIDTH  : 16                   ,
        ENTRIES: 16                   ,
        KIND   : selector_kind::BINARY,
    ) (
        i_data  : rbus_source              ,
        i_select: c_if.rbus_source_selector,
        o_data  : rbus                     ,
    );

    inst alu0: alu (
        operation: c_if.alu_operation,
        left     : lbus              ,
        right    : rbus              ,
        cin      : c_if.cin          ,
        result   : c_if.abus         ,
        cout     : c_if.alu_cout     ,
    );

    inst shifter0: shifter (
        operation: c_if.shifter_operation,
        r#in     : c_if.abus             ,
        cin      : c_if.cin              ,
        out      : sbus                  ,
        cout     : c_if.shifter_cout     ,
    );

    // logics
    assign c_if.lbus_msb   = lbus[15];
    assign c_if.rbus_msb   = rbus[15];
    assign c_if.rbus_lower = rbus[7:0];
    assign c_if.sbus_msb   = sbus[15];

    always_ff {
        if_reset {
            for i: u32 in 0..8 {
                gpr[i] = CONSTANT_ZERO_16;
            }
        } else {
            case c_if.gpr_destination_selector {
                gpr_destination_selector_t::GPR0: gpr[0]   = sbus;
                gpr_destination_selector_t::GPR1: gpr[1]   = sbus;
                gpr_destination_selector_t::GPR2: gpr[2]   = sbus;
                gpr_destination_selector_t::GPR3: gpr[3]   = sbus;
                gpr_destination_selector_t::GPR4: gpr[4]   = sbus;
                gpr_destination_selector_t::GPR5: gpr[5]   = sbus;
                gpr_destination_selector_t::GPR6: gpr[6]   = sbus;
                gpr_destination_selector_t::GPR7: gpr[7]   = sbus;
                gpr_destination_selector_t::RA  : gpr[ra]  = sbus;
                gpr_destination_selector_t::RAP : gpr[rap] = sbus;
                gpr_destination_selector_t::RB  : gpr[rb]  = sbus;
                gpr_destination_selector_t::RBP : gpr[rbp] = sbus;
                default                         : gpr[0]   = gpr[0];
            }
        }
    }

    always_ff {
        if_reset {
            pc = CONSTANT_ZERO_16;
        } else if (c_if.set_pc) {
            pc = sbus;
        } else {
            pc = pc;
        }
    }

    always_ff {
        if_reset {
            fsr = CONSTANT_ZERO_16[3:0];
        } else if (c_if.set_fsr) {
            fsr = c_if.flags;
        } else {
            fsr = fsr;
        }
    }

    always_ff {
        if_reset {
            mar = ENTRY_POINT;
        } else if (c_if.set_mar) {
            mar = lbus;
        } else {
            mar = mar;
        }
    }

    always_ff {
        if_reset {
            c_if.ir = CONSTANT_ZERO_16;
        } else {
            case c_if.ir_source_selector {
                ir_source_selector_t::SET_IR     : c_if.ir = lbus;
                ir_source_selector_t::INCREASE_RA: c_if.ir = {op, rap, rb, nd};
                ir_source_selector_t::INCREASE_RB: c_if.ir = {op, ra, rbp, nd};
                ir_source_selector_t::DECREASE_RB: c_if.ir = {op, ra, rbm, nd};
                default                          : c_if.ir = c_if.ir;
            }
        }
    }

    assign mm_addr = mar;
    assign mm_dout = lbus;
    assign of_din  = lbus[7:0];
    assign {op, ra, rb, nd} = c_if.ir;
    assign rap = c_if.ir[11:10] + 2'b1;
    assign rbp = c_if.ir[9:8] + 2'b1;
    assign rbm = c_if.ir[9:8] - 2'b1;

    assign inbus = if c_if.inbus_valid ? {CONSTANT_ZERO_16[7:0], if_dout} : CONSTANT_ZERO_16;

    always_comb {
        for i: u32 in 0..=7 {
            lbus_source[i] = gpr[i];
        }
        lbus_source[8]  = gpr[rb];
        lbus_source[9]  = gpr[rbp];
        lbus_source[10] = pc;
        lbus_source[11] = inbus;
        lbus_source[12] = mm_din;
        lbus_source[13] = c_if.ir;
        lbus_source[14] = {CONSTANT_ZERO_16[11:0], fsr};
        lbus_source[15] = CONSTANT_ZERO_16;
    }
    always_comb {
        for i: u32 in 0..=7 {
            rbus_source[i] = gpr[i];
        }
        rbus_source[8]  = gpr[ra];
        rbus_source[9]  = gpr[rap];
        rbus_source[10] = {CONSTANT_ZERO_16[15:9], c_if.literal[8:0]};
        rbus_source[11] = c_if.literal;
        for i: u32 in 12..=15 {
            rbus_source[i] = CONSTANT_ZERO_16;
        }
    }
}
