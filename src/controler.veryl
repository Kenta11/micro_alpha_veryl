module controler #(
    param ENTRY_POINT: logic<12> = 0,
) (
    clk     : input   clock                 ,
    rst     : input   reset                 ,
    c_if    : modport control_if::controler ,
    cm_addr : output  MICRO1_CONTROL_ADDRESS,
    cm_din  : input   MICRO1_CONTROL_WORD   ,
    mm_we   : output  logic                 ,
    if_empty: input   logic                 ,
    if_re   : output  logic                 ,
    of_full : input   logic                 ,
    of_we   : output  logic                 ,
    led_hlt : output  logic                 ,
    led_ov  : output  logic                 ,
) {
    // imports
    import alu_pkg::*;
    import control_data_pkg::*;
    import gpr_destination_selector_pkg::*;
    import ir_source_selector_pkg::*;
    import lbus_source_selector_pkg::*;
    import rbus_source_selector_pkg::*;
    import shifter_pkg::*;

    // type definitions
    enum phase_t {
        INIT = 3'h0,
        T1 = 3'h1,
        T2 = 3'h2,
        T3 = 3'h3,
        T4 = 3'h4,
        T5 = 3'h5,
        HLT = 3'h6,
    }
    enum sb_t {
        R0 = 4'h0,
        R1 = 4'h1,
        R2 = 4'h2,
        R3 = 4'h3,
        R4 = 4'h4,
        R5 = 4'h5,
        R6 = 4'h6,
        R7 = 4'h7,
        RA = 4'h8,
        RAP = 4'h9,
        RB = 4'hA,
        RBP = 4'hB,
        PC = 4'hC,
        NONE = 4'hF,
    }
    enum mm_t {
        RM = 2'h0,
        WM = 2'h1,
        NMM = 2'h3,
    }
    enum sq_t {
        B = 4'h0,
        BP = 4'h1,
        RTN = 4'h2,
        BT = 4'h3,
        BF = 4'h4,
        IOP = 4'h5,
        IRA = 4'h6,
        IAB = 4'h7,
        EI = 4'h8,
        NSQ = 4'hF,
    }
    enum ts_t {
        ZER = 3'h0,
        NEG = 3'h1,
        CRY = 3'h2,
        OV = 3'h3,
        T = 3'h4,
        CZ = 3'h5,
        NTS = 3'h7,
    }
    enum ex_t {
        CM1 = 4'h0,
        FLS = 4'h1,
        ASC = 4'h2,
        AS1 = 4'h3,
        LIR = 4'h4,
        LIO = 4'h5,
        SC = 4'h6,
        EIO = 4'h7,
        ST = 4'h8,
        RT = 4'h9,
        INA = 4'hA,
        INB = 4'hB,
        DCB = 4'hC,
        HLT = 4'hD,
        OV = 4'hE,
        NEX = 4'hF,
    }

    // constant value
    const CONSTANT_ZERO_40: logic<40> = 40'h0;
    const FLAG_INDEX_ZER  : logic<3>  = 3'h4;
    const FLAG_INDEX_NEG  : logic<3>  = 3'h3;
    const FLAG_INDEX_CRY  : logic<3>  = 3'h2;
    const FLAG_INDEX_OV   : logic<3>  = 3'h1;
    const FLAG_INDEX_T    : logic<3>  = 3'h0;

    // registers and wires
    var phase               : phase_t                  ;
    var cmar                : MICRO1_CONTROL_ADDRESS   ;
    var cmdr                : MICRO1_CONTROL_WORD      ;
    var cmar_stack          : MICRO1_CONTROL_WORD   <8>;
    var cmar_stack_pointer  : logic                 <3>;
    var mm_write_reservation: logic                    ;
    var c                   : logic                 <8>;
    var flags               : logic                 <6>;
    var flag_zer            : logic                    ;
    var flag_neg            : logic                    ;
    var flag_cry            : logic                    ;
    var flag_ov             : logic                    ;
    var flag_cz             : logic                    ;
    var flag_t              : logic                    ;
    var test                : logic                    ;
    var lb                  : lbus_source_selector_t   ;
    var rb                  : rbus_source_selector_t   ;
    var al                  : alu_operation_t          ;
    var sh                  : shifter_operation_t      ;
    var sb                  : sb_t                     ;
    var mm                  : mm_t                     ;
    var sq                  : sq_t                     ;
    var ts                  : logic                 <3>;
    var ex                  : ex_t                     ;
    var lt                  : logic                 <9>;

    // logics
    assign c_if.lbus_source_selector = lb;
    assign c_if.rbus_source_selector = rb;

    assign c_if.alu_operation = case al {
        alu_operation_t::IAL: case c_if.ir[14:12] {
            3'b000 : alu_operation_t::ADD,
            3'b001 : alu_operation_t::SUB,
            3'b010 : alu_operation_t::AND,
            3'b011 : alu_operation_t::OR,
            3'b100 : alu_operation_t::XOR,
            default: alu_operation_t::NOP,
        },
        default: al,
    };

    assign c_if.shifter_operation = sh;

    assign c_if.gpr_destination_selector = case phase {
        phase_t::T5: case sb {
            sb_t::R0 : gpr_destination_selector_t::GPR0,
            sb_t::R1 : gpr_destination_selector_t::GPR1,
            sb_t::R2 : gpr_destination_selector_t::GPR2,
            sb_t::R3 : gpr_destination_selector_t::GPR3,
            sb_t::R4 : gpr_destination_selector_t::GPR4,
            sb_t::R5 : gpr_destination_selector_t::GPR5,
            sb_t::R6 : gpr_destination_selector_t::GPR6,
            sb_t::R7 : gpr_destination_selector_t::GPR7,
            sb_t::RA : gpr_destination_selector_t::RA,
            sb_t::RAP: gpr_destination_selector_t::RAP,
            sb_t::RB : gpr_destination_selector_t::RB,
            sb_t::RBP: gpr_destination_selector_t::RBP,
            default  : gpr_destination_selector_t::NONE,
        },
        default: gpr_destination_selector_t::NONE,
    };

    assign c_if.ir_source_selector = case phase {
        phase_t::T3: case ex {
            ex_t::LIR: ir_source_selector_t::SET_IR,
            default  : ir_source_selector_t::NO_OPERATION,
        },
        phase_t::T5: case ex {
            ex_t::INA: ir_source_selector_t::INCREASE_RA,
            ex_t::INB: ir_source_selector_t::INCREASE_RB,
            ex_t::DCB: ir_source_selector_t::DECREASE_RB,
            default  : ir_source_selector_t::NO_OPERATION,
        },
        default: ir_source_selector_t::NO_OPERATION,
    };

    assign c_if.set_mar = case phase {
        phase_t::T2: case mm {
            mm_t::RM: 1'b1,
            mm_t::WM: 1'b1,
            default : 1'b0,
        },
        default: 1'b0,
    };

    assign c_if.set_fsr = case phase {
        phase_t::T5: case ex {
            ex_t::FLS: 1'b1,
            default  : 1'b0,
        },
        default: 1'b0,
    };

    assign c_if.set_pc = case phase {
        phase_t::T5: case sb {
            sb_t::PC: 1'b1,
            default : 1'b0,
        },
        default: 1'b0,
    };

    assign c_if.literal = {ts, ex, lt};
    assign c_if.flags   = {flag_zer, flag_neg, flag_cry, flag_ov};

    assign c_if.cin = case ex {
        ex_t::ASC: flags[FLAG_INDEX_CRY],
        ex_t::AS1: 1'h1,
        default  : 1'h0,
    };

    always_ff {
        if_reset {
            c_if.inbus_valid = 1'b0;
        } else {
            case phase {
                phase_t::T2: case ex {
                    ex_t::EIO: {
                        if (c_if.ir == 16'hEC00) {
                            c_if.inbus_valid = ~if_empty;
                        } else {
                            c_if.inbus_valid = c_if.inbus_valid;
                        }
                    }
                    default: c_if.inbus_valid = c_if.inbus_valid;
                }
                default: c_if.inbus_valid = c_if.inbus_valid;
            }
        }
    }

    assign cm_addr = cmar;

    assign mm_we = case phase {
        phase_t::T2: mm_write_reservation,
        default    : 1'h0,
    };

    assign if_re = case phase {
        phase_t::T2: case ex {
            ex_t::EIO: (c_if.ir == 16'hEC00) && (~if_empty),
            default  : 1'b0,
        },
        default: 1'b0,
    };

    assign of_we = case phase {
        phase_t::T3: case ex {
            ex_t::LIO: (c_if.ir == 16'hED01) && (~of_full),
            default  : 1'b0,
        },
        default: 1'b0,
    };

    always_ff {
        if_reset {
            led_hlt = 1'h0;
        } else {
            case phase {
                phase_t::T5: case ex {
                    ex_t::HLT: led_hlt = 1'h1;
                    default  : led_hlt = 1'h0;
                }
                default: led_hlt = led_hlt;
            }
        }
    }

    always_ff {
        if_reset {
            led_ov = 1'h0;
        } else {
            case phase {
                phase_t::T5: case ex {
                    ex_t::OV: led_ov = 1'h1;
                    default : led_ov = 1'h0;
                }
                default: led_ov = led_ov;
            }
        }
    }

    always_ff {
        if_reset {
            phase = phase_t::INIT;
        } else {
            case phase {
                phase_t::INIT: phase = phase_t::T1;
                phase_t::T1  : phase = phase_t::T2;
                phase_t::T2  : phase = phase_t::T3;
                phase_t::T3  : phase = phase_t::T4;
                phase_t::T4  : phase = phase_t::T5;
                phase_t::T5  : case ex {
                    ex_t::HLT: phase = phase_t::HLT;
                    ex_t::OV : phase = phase_t::HLT;
                    default  : phase = phase_t::T1;
                }
                default: phase = phase;
            }
        }
    }

    always_ff {
        if_reset {
            cmar = ENTRY_POINT;
        } else {
            case phase {
                phase_t::T2: case sq {
                    sq_t::B  : cmar = {ts, lt};
                    sq_t::BP : cmar = {ts, lt};
                    sq_t::RTN: cmar = cmar_stack[cmar_stack_pointer - 3'h1];
                    sq_t::BT : cmar = if test ? {cmar[11:9], lt} : (cmar + 12'h1);
                    sq_t::BF : cmar = if test ? (cmar + 12'h1) : {cmar[11:9], lt};
                    sq_t::IOP: cmar = {lt[7:0], c_if.ir[15:12]};
                    sq_t::IRA: cmar = {ts[0], lt[8:2], c_if.ir[11:10], lt[1:0]};
                    sq_t::IAB: cmar = {ts[0], lt[8:4], c_if.ir[11:8], lt[1:0]};
                    sq_t::EI : cmar = if test ? CONSTANT_ZERO_40[11:0] : {cmar[11:9], lt};
                    default  : cmar = cmar + 12'h1;
                }
                default: cmar = cmar;
            }
        }
    }

    always_ff {
        if_reset {
            cmdr = {
                lbus_source_selector_t::NLB,
                rbus_source_selector_t::NRB,
                alu_operation_t::NOP,
                shifter_operation_t::NOP,
                sb_t::NONE,
                mm_t::NMM,
                sq_t::NSQ,
                ts_t::NTS,
                ex_t::NEX,
                9'h0
            };
        } else {
            case phase {
                phase_t::INIT: cmdr = cm_din;
                phase_t::T5  : cmdr = cm_din;
                default      : cmdr = cmdr;
            }
        }
    }

    always_ff {
        if_reset {
            for i: u32 in 0..8 {
                cmar_stack[i] = CONSTANT_ZERO_40[11:0];
            }
        } else {
            case phase {
                phase_t::T2: case sq {
                    sq_t::BP: cmar_stack[cmar_stack_pointer] = cmar;
                    default : cmar_stack[cmar_stack_pointer] = cmar_stack[cmar_stack_pointer];
                }
                default: cmar_stack[cmar_stack_pointer] = cmar_stack[cmar_stack_pointer];
            }
        }
    }

    always_ff {
        if_reset {
            cmar_stack_pointer = 3'h0;
        } else {
            case phase {
                phase_t::T2: case sq {
                    sq_t::BP : cmar_stack_pointer = cmar_stack_pointer + 3'h1;
                    sq_t::RTN: cmar_stack_pointer = cmar_stack_pointer - 3'h1;
                    default  : cmar_stack_pointer = cmar_stack_pointer;
                }
                default: cmar_stack_pointer = cmar_stack_pointer;
            }
        }
    }

    always_ff {
        if_reset {
            mm_write_reservation = 1'h0;
        } else {
            case phase {
                phase_t::T2: mm_write_reservation = mm == mm_t::WM;
                default    : mm_write_reservation = mm_write_reservation;
            }
        }
    }

    always_ff {
        if_reset {
            c = CONSTANT_ZERO_40[7:0];
        } else {
            case phase {
                phase_t::T5: case ex {
                    ex_t::CM1: c = c - 8'h1;
                    ex_t::SC : c = c_if.rbus_lower;
                    default  : c = c;
                }
                default: c = c;
            }
        }
    }

    always_ff {
        if_reset {
            flags = 6'h0;
        } else {
            case phase {
                phase_t::T5: flags = {flag_zer, flag_neg, flag_cry, flag_ov, flag_t};
                default    : flags = flags;
            }
        }
    }

    assign flag_zer = case al {
        alu_operation_t::ADD: c_if.abus == CONSTANT_ZERO_40[15:0],
        alu_operation_t::SUB: c_if.abus == CONSTANT_ZERO_40[15:0],
        alu_operation_t::AND: c_if.abus == CONSTANT_ZERO_40[15:0],
        alu_operation_t::OR : c_if.abus == CONSTANT_ZERO_40[15:0],
        default             : 1'h0,
    };

    assign flag_neg = case al {
        alu_operation_t::ADD: c_if.abus[15],
        alu_operation_t::SUB: c_if.abus[15],
        default             : 1'h0,
    };

    assign flag_cry = case c_if.shifter_operation {
        shifter_operation_t::LEFT_LOGICALLY      : c_if.shifter_cout,
        shifter_operation_t::RIGHT_LOGICALLY     : c_if.shifter_cout,
        shifter_operation_t::LEFT_ARITHMETICALLY : c_if.shifter_cout,
        shifter_operation_t::RIGHT_ARITHMETICALLY: c_if.shifter_cout,
        shifter_operation_t::EXTENSION           : c_if.shifter_cout,
        default                                  : case al {
            alu_operation_t::ADD: c_if.alu_cout,
            alu_operation_t::SUB: c_if.alu_cout,
            default             : c_if.alu_cout,
        },
    };

    assign flag_ov = case c_if.shifter_operation {
        shifter_operation_t::LEFT_ARITHMETICALLY: c_if.abus[15] ^ c_if.sbus_msb,
        default                                 : case al {
            alu_operation_t::ADD: (c_if.lbus_msb == c_if.rbus_msb) ^ c_if.abus[15],
            alu_operation_t::SUB: (c_if.lbus_msb | (~c_if.rbus_msb) | c_if.abus[15]) | ((~c_if.lbus_msb) | c_if.rbus_msb | (~c_if.abus[15])),
            default             : 1'h0,
        },
    };

    assign flag_cz = c == 8'h0;

    assign flag_t = case ex {
        ex_t::ST: 1'h1,
        ex_t::RT: 1'h0,
        default : 1'h0,
    };

    assign test = case ts {
        ts_t::ZER: flags[FLAG_INDEX_ZER],
        ts_t::NEG: flags[FLAG_INDEX_NEG],
        ts_t::CRY: flags[FLAG_INDEX_CRY],
        ts_t::OV : flags[FLAG_INDEX_OV],
        ts_t::T  : flags[FLAG_INDEX_T],
        ts_t::CZ : flag_cz,
        default  : 1'h1,
    };

    assign {lb, rb, al, sh, sb, mm, sq, ts, ex, lt} = cmdr;
}
