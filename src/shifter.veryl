module shifter (
    operation: input  shifter_operation_t  ,
    r#in     : input  micro1_machine_word_t,
    cin      : input  logic                ,
    out      : output micro1_machine_word_t,
    cout     : output logic                ,
) {
    // imports
    import machine_data_pkg::*;
    import shifter_pkg::*;

    assign {cout, out} = case operation {
        shifter_operation_t::LEFT_LOGICALLY      : {r#in, cin},
        shifter_operation_t::RIGHT_LOGICALLY     : {r#in[0], cin, r#in[15:1]},
        shifter_operation_t::LEFT_ARITHMETICALLY : {r#in[15], r#in[15], r#in[13:0], cin},
        shifter_operation_t::RIGHT_ARITHMETICALLY: {r#in[0], r#in[15], r#in[15:1]},
        shifter_operation_t::EXTENSION           : {r#in[7], r#in[7] repeat 8, r#in[7:0]},
        shifter_operation_t::SWAP                : {1'b0, r#in[7:0], r#in[15:8]},
        default                                  : {1'b0, r#in},
    };
}
